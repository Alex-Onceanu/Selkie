#version 460
#extension GL_EXT_ray_tracing : require

layout(set = 0, binding = 0) uniform accelerationStructureEXT bvh;
layout(set = 0, binding = 1, rgba8) uniform image2D image;

#include "main_payload.glsl"

layout(location = 0) rayPayloadEXT payload_t payload;

layout(push_constant) uniform PushConstants {
    float time;
};

mat2 rot2D(const float theta)
{
    return mat2(vec2(cos(theta), -sin(theta)), vec2(sin(theta), cos(theta)));
}

#define FOV (70. * 3.1416 / 180.)

void main()
{
    vec2 uv = vec2(gl_LaunchIDEXT.xy) / vec2(gl_LaunchSizeEXT.xy);
    uv = 2. * uv - 1.;    // uv is in [-1, 1]
    uv.x *= 1366. / 768.; // aspect ratio
    uv.y = - uv.y;        // x goes right, y goes up, z towards me

    vec3 cameraPos = vec3(0., 1.2, 9.);
    vec3 rayDir = vec3(uv.x, uv.y, -2. / tan(0.5 * FOV));
    cameraPos.xz *= rot2D(0.1 * time);
    rayDir.xz *= rot2D(0.1 * time);
    
    payload.nbHits = 0;

    traceRayEXT(bvh, gl_RayFlagsSkipClosestHitShaderEXT, 0xFF, 0, 0, 0, cameraPos, T_MIN, rayDir, T_MAX, 0);

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(payload.hitColor, 0.0));
}
